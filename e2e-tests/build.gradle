/*
 * ConnectBot: simple, powerful, open-source SSH client for Android
 * Copyright 2018 Kenny Root, Jeffrey Sharkey
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.palantir.python.miniconda' version '0.8.0'
}

apply plugin: 'com.palantir.python.miniconda'

miniconda {
    minicondaVersion = '4.5.1'
    pythonVersion = 3
    packages = ['python']
}

def pythonExec = "${miniconda.buildEnvironmentDirectory}/bin/python"
def pipExec = "${miniconda.buildEnvironmentDirectory}/bin/pip"

task installRequirements(type: Exec) {
    dependsOn 'setupPython'
    executable pipExec
    args 'install', '-r', file('./requirements.txt')
}

evaluationDependsOn(':app')

task sshE2eTest(type: Exec) {
    dependsOn 'installRequirements', project(':app').tasks.getByPath('installSnippetDebug')
    executable pythonExec
    args file('src/main/python/ssh_e2e_test.py'), '-c', file('./src/main/python/ssh_config.yml')
}

task connectedCheck {
    dependsOn 'sshE2eTest'
}
