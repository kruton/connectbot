// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    project.ext.TRANSLATIONS_ONLY = System.getenv("TRANSLATIONS_ONLY")?.trim()
    apply from: "versions.gradle"
    addRepos(repositories)
}

plugins { id "com.diffplug.gradle.spotless" version "3.16.0" }

allprojects {
    buildscript {
        addRepos(repositories)
    }
    addRepos(repositories)
}

if (!TRANSLATIONS_ONLY) {
    apply from: 'spotless.gradle'
}

tasks.register("resolveDependencies") {
    setDescription "Resolves all projects dependencies from the repository."
    setGroup "Build Server"

    doLast {
        rootProject.allprojects { project ->
            project.buildscript.configurations.forEach { configuration ->
                if (configuration.canBeResolved) {
                    configuration.resolve()
                }
            }

            project.configurations.forEach { configuration ->
                if (configuration.canBeResolved) {
                    configuration.resolve()
                }
            }
        }
    }
}
